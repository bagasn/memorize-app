apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def versionFile = rootProject.file("version.properties")
def mVersionCode, mVersionApp, mVersionRelease, mVersionDebug
def mVersionName
def mDatabaseVersion = 1

if (versionFile.canRead()) {
    def versionProperties = new Properties()
    versionProperties.load(new FileInputStream(versionFile))

    mVersionCode = versionProperties['VERSION_CODE']
    mVersionApp = versionProperties['APP_VERSION']
    mVersionRelease = versionProperties['RELEASE_VERSION']
    mVersionDebug = versionProperties['DEBUG_VERSION']

    mVersionName = mVersionApp + "." + mVersionRelease + "." + mVersionDebug
} else {
    throw new GradleException("Couldn't read version.properties")
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "me.bndev.memorizeapps"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode mVersionCode.toInteger()
        versionName mVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = "Memorize App v${variant.versionName} vdb-$mDatabaseVersion -${variant.name}"
            }
        }
    }
}

ext.increaseVersionRelease = {
    if (versionFile.canRead()) {
        def versionProperties = new Properties()
        versionProperties.load(new FileInputStream(versionFile))

        def versionCode = versionProperties["VERSION_CODE"].toInteger() + 1
        def releaseVersion = versionProperties["RELEASE_VERSION"].toInteger() + 1

        versionProperties["VERSION_CODE"] = versionCode.toString()
        versionProperties["RELEASE_VERSION"] = releaseVersion.toString()
        versionProperties["DEBUG_VERSION"] = "0"

        versionProperties.store(versionFile.newWriter(), null)
    } else {
        throw new GradleException("Error while increase version release")
    }
}

ext.increaseVersionDebug = {
    if (versionFile.canRead()) {
        def versionProperties = new Properties()
        versionProperties.load(new FileInputStream(versionFile))

        def debugVersion = versionProperties["DEBUG_VERSION"].toInteger() + 1
        versionProperties["DEBUG_VERSION"] = debugVersion.toString()

        versionProperties.store(versionFile.newWriter(), null)
    } else {
        throw new GradleException("Error while increase version debug")
    }
}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(assembleDebug)) {
        increaseVersionDebug()
    } else if (taskGraph.hasTask(assembleRelease)) {
        increaseVersionRelease()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
}
